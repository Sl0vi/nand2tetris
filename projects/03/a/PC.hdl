// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t]==1) out[t+1] = 0
 * else if (load[t]==1)  out[t+1] = in[t]
 * else if (inc[t]==1)   out[t+1] = out[t] + 1  (integer addition)
 * else                  out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // The stored value
	Register(in=resetIn, load=updateRegister, out=out, out=registerValue);

	// Should the stored value be updated?
    Or(a=load, b=inc, out=u0);
    Or(a=u0, b=reset, out=updateRegister);

    // Increment
    Inc16(in=registerValue, out=i0);
    Mux16(a=in, b=i0, sel=inc, out=incrementedIn);

    // Load
    Mux16(a=incrementedIn, b=in, sel=load, out=loadedIn);

    // Reset
    Mux16(a=loadedIn, b=false, sel=reset, out=resetIn);
}